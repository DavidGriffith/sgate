Inside Hall "Hall"
	with name "hall",
	description [;
		print "You stand in a long corridor.  Huge stone
		archways line the entire hall.  At the north end of
		this room is an impressive hall door covered with
		sheets of steel.  A small door is to the east. ";

		if (self hasnt visited) {
"^^A huge pair of disembodied eyes appear high in the air above
you, glaring angrily.  The booming voice of the Warlock Lord bursts
forth from nowhere.^
^
~That pitiful wizard Lakmir was a fool to send a buffoon like you to
stop me.  You will surely regret it for the only thing here for you is
a horrible death!~^
^
The eyes fade away as if made of smoke.  Maniacal laughter echoes in
your ears.";
		}
		"";
	],
	n_to HallDoor,
	e_to HallClosetDoor,
	s_to OuterDoor,
	cant_go "You can only go north, east, or south.";

UnlitTorch ->;
UnlitTorch ->;

Rug -> HallRug "rug";


Object -> HallArches "archways"
	with name "huge" "stone" "archway" "archways" "arch" "arches",
	description "They're quite impressive.",
	has scenery pluralname;


ObligingDoor -> HallClosetDoor "small door"
	with name "closet" "door" "small" "narrow",
	description "The small door is very narrow.  Even
		though it's only an inch thick, it is very sturdy.",
	door_to [;
		if (self in Hall) {
			return HallCloset;
		}
		return Hall;
	],
	door_dir [;
		if (self in Hall) {
			return e_to;
		}
		return w_to;
	],
	before [;
	Search, LookUnder:
		"Perhaps entering the closet with be more
		useful (it's actually a walk-in closet).";
	],
	found_in Hall HallCloset,
	with_key skullkey,
	has scenery door openable lockable locked;


ObligingDoor -> HallDoor "hall door"
	with name "hall" "door" "steel",
	description "This door is reinforced with heavy sheets 
		of steel.",
	door_to [;
		if (self in Hall)
			return StonePassage;
		return Hall;
	],
	door_dir [;
		if (self in Hall)
			return n_to;
		return s_to;
	],
	found_in Hall StonePassage,
	with_key ironkey,
	has scenery door openable lockable locked;


