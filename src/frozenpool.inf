Inside FrozenPool "Frozen pool"
	with name "frozen" "pool",
	description "The pool has become a solid sheet of ice.  It's a 
		bit difficult to walk here.  The shore is to the
		north.",
	n_to PoolCavern,
	cant_go "There's nowhere else to go but north.";

Object -> Ice "ice"
	with name "ice",
	description "The pool is frozen solid.",
	has scenery;

Object -> IceShore "shore"
	with name "shore" "beach",
	description "A coarse, sandy beach.",
	found_in FrozenPool PoolCavern,
	has scenery;

Object -> IceSpike "spike"
	with name "spike",
	description "It's a rocky spike sticking out of the ice.  A
		lime-covered skeleton is perched upon it.",
	before [;
	Receive:
		print (The) noun, " slides off.^";
		move noun to parent(self);
		rtrue;
	],
	capacity 0,
	has supporter scenery;

Object -> LimeSkeleton "skeleton"
	with name "lime-covered" "lime" "covered" "skeleton",
	describe [;
		if (parent(self) hasnt general) {
			print "^A lime-covered skeleton is perched on a
			rocky spike sticking out of the ice.";
		}
		if (BrassKey in self) {
			print "  It clutches a brass key.";
		}
		"";
	],
	description [;
		print "The lime-covered skeleton stares at your through
		eyeless sockets.";
		if (BrassKey in self) {
			print "It clutches a brass key";
		}
		".";
	],
	before [;
	Take, Transfer, Push, Pull, Slide:
		"Leave him to rest in peace.";
	LetGo:
		remove PoolKey;
	],
	has supporter static;

! key3
!
Key -> -> BrassKey "brass key"
	with name "brass",
	short_name "brass key",
	description "It's a small brass key.",
	has is_tiny;

